function inventorySystem::insertSaveData(%this)
{
    $saveRecord.add("<inventorySystem>","");
    
   for (%clientIndex = 0; %clientIndex < ClientGroup.getCount(); %clientIndex++)
   {
      %client = ClientGroup.getObject(%clientIndex); 
      %count = %client.player.inventoryArray.count();
      if (%count<1) $saveRecord.add("","");
      for(%i=0; %i < %count; %i++)
      {
           %inventoryItemName = %client.player.inventoryArray.getKey(%i);
           %inventoryAmount = %client.player.inventoryArray.getValue(%i);
           
           if(%inventoryAmount <= 0)
               continue;
           
           %inventoryListInfo = %inventoryItemName TAB %inventoryAmount;
           
           $saveRecord.add(%client.connectData, %inventoryListInfo);
      } 
   }
}

function inventorySystem::parseSaveData(%this)
{
    %count = $saveRecord.count();
    %curModule = "";
    for (%i=0;%i<%count;%i++)
    {
        %temp = $saveRecord.getKey(%i);
        if (strchrpos(getword(%temp,0),"<")>-1)
        {
            %curModule = stripChars(%temp,"< >");
            %i++;//skip module divide entries
        }
        if (%curModule $= "inventorySystem")
        {
            %curChar = $saveRecord.getKey(%i);
            %inventoryItemName = getWord($saveRecord.getValue(%i), 0);
            %inventoryAmount = getWord($saveRecord.getValue(%i), 1);
                   
            if(!isObject(%this.charRecord[%curChar,inventoryArray]))
            {
                %this.charRecord[%curChar,inventoryArray] = new ArrayObject();
            }
            %this.charRecord[%curChar,inventoryArray].add(%inventoryItemName, %inventoryAmount);
        }
    }
}


function inventorySystem::getConnectionSave(%this, %client)
{
    if (!isObject(%this.charRecord[%client.connectData,inventoryArray])) return;
    %client.player.clearInventory();
    %count = %this.charRecord[%client.connectData,inventoryArray].count();
    for (%i=0;%i<%count;%i++)
    {
        %inventoryItemName = %this.charRecord[%client.connectData,inventoryArray].getKey(%i);
        %inventoryAmount = %this.charRecord[%client.connectData,inventoryArray].getValue(%i);
        %client.player.setInventory(%inventoryItemName, %inventoryAmount);
    }
}