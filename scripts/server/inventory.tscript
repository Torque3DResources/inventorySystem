//-----------------------------------------------------------------------------
// Copyright (c) 2012 GarageGames, LLC
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to
// deal in the Software without restriction, including without limitation the
// rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
// sell copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
// FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
// IN THE SOFTWARE.
//-----------------------------------------------------------------------------

// This inventory system is totally scripted, no C++ code involved.
// It uses object datablock names to track inventory and is generally
// object type, or class, agnostic.  In other words, it will inventory
// any kind of ShapeBase object, though the throw method does assume
// that the objects are small enough to throw :)
//
// For a ShapeBase object to support inventory, it must have an array
// of inventory max values:
//    %this.maxInv[GunAmmo] = 100;
//    %this.maxInv[SpeedGun] = 1;
// where the names "SpeedGun" and "GunAmmo" are datablocks.
//
// For objects to be inventoriable, they must provide a set of inventory
// callback methods, mainly:
//    onUse
//    onThrow
//    onPickup
//
// Example methods are given further down.  The item.cs file also contains
// example inventory items.

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------
// Inventory server commands
//-----------------------------------------------------------------------------

function serverCmdUse(%client, %itemData)
{
   %client.getControlObject().use(%itemData);
}

//-----------------------------------------------------------------------------
// ShapeBase inventory support
//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

function ShapeBase::use(%this, %itemData)
{
   // Use an object in the inventory.

   // Need to prevent weapon changing when zooming, but only shapes
   // that have a connection.
   %conn = %this.getControllingClient();
   if (%conn)
   {
      %defaultFov = %conn.getControlCameraDefaultFov();
      %fov = %conn.getControlCameraFov();
      if (%fov != %defaultFov)
         return false;
   }

   if (%this.getInventory(%itemData) > 0)
      return %itemData.onUse(%this);

   return false;
}

function ShapeBase::throw(%this, %itemData, %amount)
{
   // Throw objects from inventory. The onThrow method is
   // responsible for decrementing the inventory.

   if (%this.getInventory(%itemData) > 0)
   {
      %itemInst = %itemData.onThrow(%this, %amount);
      if (%itemInst)
      {
         %this.throwObject(%itemInst);
         serverPlay3D(ThrowSnd, %this.getTransform());
         return true;
      }
   }
   return false;
}

function ShapeBase::pickup(%this, %itemInst, %amount)
{
   // This method is called to pickup an object and add it to the inventory.
   // The datablock onPickup method is actually responsible for doing all the
   // work, including incrementing the inventory.

   %itemData = %itemInst.getDatablock();

   // Try and pickup the max if no value was specified
   if (%amount $= "")
      %amount = %this.maxInventory(%itemData) - %this.getInventory(%itemData);

   // The datablock does the work...
   if (%amount < 0)
      %amount = 0;
   if (%amount)
      return %itemData.onPickup(%itemInst, %this, %amount);
   return false;
}

//-----------------------------------------------------------------------------

function ShapeBase::hasInventory(%this, %itemData)
{
   return (%this.getInventory(%itemData) > 0);
}

function ShapeBase::hasAmmo(%this, %weapon)
{
   if (%weapon.image.ammo $= "")
      return(true);
   else
      return(%this.getInventory(%weapon.image.ammo) > 0);
}

function ShapeBase::maxInventory(%this, %itemData)
{
    //%this is the object-instance holding the item
    // see if that instance datablock has a cap. 0 denotes no limit, -1 denotes can't use
    if (%this.getDatablock().maxInv[%itemData.getName()] != -1)
    {
        return (%this.getDatablock().maxInv[%itemData.getName()]);
    }
    else if (%itemData.maxInventory != -1) // check the item datablock to see if *it* specifies a cap
    {   
        return %itemData.maxInventory;
    }
    return 0;
}

function ShapeBase::incInventory(%this, %itemData, %amount)
{
   // Increment the inventory by the given amount.  The return value
   // is the amount actually added, which may be less than the
   // requested amount due to inventory restrictions.

   %max = %this.maxInventory(%itemData);
   %total = %this.getInventory(%itemData);
   %netAmmount = %total + %amount;
   if (%total < %max || %max == 0)
   {
      if (%netAmmount > %max)
         %netAmmount = %max;
      %this.setInventory(%itemData, %netAmmount);
      return %netAmmount;
   }
   return 0;
}

function ShapeBase::decInventory(%this, %itemData, %amount)
{
   // Decrement the inventory by the given amount. The return value
   // is the amount actually removed.
   
   %max = %this.maxInventory(%itemData);
   %total = %this.getInventory(%itemData);
   if (%total > 0 && %max != 0)
   {
      if (%total < %amount)
         %amount = %total;
      %this.setInventory(%itemData, %total - %amount);
      return %amount;
   }
   return 0;
}

//-----------------------------------------------------------------------------

function ShapeBase::getInventory(%this, %itemData)
{
   if(!isObject(%this.inventoryArray))
   {
      %this.inventoryArray = new ArrayObject();
   }
   
   // Return the current inventory amount
   %invIdx = %this.inventoryArray.getIndexFromKey(%itemData.getName());
   %amount = %this.inventoryArray.getValue(%invIdx);
   
   if(%amount < 0)
      %amout = 0;
      
   return %amount;
}

function ShapeBase::setInventory(%this, %itemData, %value)
{
   if(!isObject(%this.inventoryArray))
   {
      %this.inventoryArray = new ArrayObject();
   }
   
   // Set the inventory amount for this datablock and invoke inventory
   // callbacks.  All changes to inventory go through this single method.

   // Impose inventory limits
   if (%value < 0)
      %value = 0;
   else
   {
      %max = %this.maxInventory(%itemData);
      if (%value > %max)
         %value = %max;
   }

   // Set the value and invoke object callbacks
   %currentAmount = %this.getInventory(%itemData);
   if (%currentAmount != %value)
   {
      %name = %itemData.getName();
      %invIdx = %this.inventoryArray.getIndexFromKey(%name);
      
      if(%invIdx == -1)
         %this.inventoryArray.add(%name, %value);
      else
         %this.inventoryArray.setValue(%value, %invIdx);
         
      %itemData.onInventory(%this, %value);
      %this.getDataBlock().onInventory(%itemData, %value);
   }
   callonModules("onSetInventory", %this, %itemData, %currentAmount); //%this is the object-instance holding the item
   return %value;
}

//-----------------------------------------------------------------------------

function ShapeBase::clearInventory(%this)
{
   if(!isObject(%this.inventoryArray))
   {
      %this.inventoryArray = new ArrayObject();
   }
   %this.inventoryArray.empty();
   callonModules("onClearInventory", %this);
}

//-----------------------------------------------------------------------------

function ShapeBase::throwObject(%this, %itemInst)
{
   // Throw the given object in the direction the shape is looking.
   // The force value is hardcoded according to the current default
   // object mass and mission gravity (20m/s^2).

   %throwForce = %this.getDataBlock().throwForce;
   if (!%throwForce)
      %throwForce = 20;

   // Start with the shape's eye vector...
   %eye = %this.getEyeVector();
   %vec = vectorScale(%eye, %throwForce);

   // Add a vertical component to give the object a better arc
   %verticalForce = %throwForce / 2;
   %dot = vectorDot("0 0 1", %eye);
   if (%dot < 0)
      %dot = -%dot;
   %vec = vectorAdd(%vec, vectorScale("0 0 "@%verticalForce, 1 - %dot));

   // Add the shape's velocity
   %vec = vectorAdd(%vec, %this.getVelocity());

   // Set the object's position and initial velocity
   %pos = getBoxCenter(%this.getWorldBox());
   %itemInst.setTransform(%pos);
   %itemInst.applyImpulse(%pos, %vec);

   // Since the object is thrown from the center of the shape,
   // the object needs to avoid colliding with it's thrower.
   %itemInst.setCollisionTimeout(%this);
}

//-----------------------------------------------------------------------------
// Callback hooks invoked by the inventory system
//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------
// ShapeBase object callbacks invoked by the inventory system

function ShapeBase::onInventory(%this, %data, %value)
{
   // Invoked on ShapeBase objects whenever their inventory changes
   // for the given datablock.
}

//-----------------------------------------------------------------------------
// ShapeBase datablock callback invoked by the inventory system.

function ShapeBaseData::onUse(%this, %user)
{
   // Invoked when the object uses this datablock, should return
   // true if the item was used.

   return false;
}

function ShapeBaseData::onThrow(%this, %user, %amount)
{
   // Invoked when the object is thrown.  This method should
   // construct and return the actual mission object to be
   // physically thrown.  This method is also responsible for
   // decrementing the user's inventory.

   return 0;
}

function ShapeBaseData::onPickup(%this, %obj, %user, %amount)
{
   // Invoked when the user attempts to pickup this datablock object.
   // The %amount argument is the space in the user's inventory for
   // this type of datablock.  This method is responsible for
   // incrementing the user's inventory is something is addded.
   // Should return true if something was added to the inventory.

   return false;
}

function ShapeBaseData::onInventory(%this, %user, %value)
{
   // Invoked whenever an user's inventory total changes for
   // this datablock.
}
